task generateFeature {
    doLast {
        if (!rootProject.hasProperty('featureName')) {
            logger.error('ERROR: Please provide the feature name: ./gradlew generateFeature -PfeatureName=MyFeature')
            return
        }

        featureName.metaClass.toSnakeCase = { replaceAll(/([A-Z])/, /_$1/).toLowerCase().replaceAll(/^_/, '') }

        def placeholders = [
                'PlaceholderName'        : featureName.capitalize(),
                'placeholderUncaptalized': featureName.uncapitalize(),
                'PLACEHOLDER_UPPERCASE'  : featureName.toSnakeCase().toUpperCase(),
                'placeholderlowercase'   : featureName.toLowerCase(),
                'placeholder_snake_case' : featureName.toSnakeCase()
        ]

        def placeholdersUsed = placeholders.collect { k, v -> "            ${k}: ${v}" }.join("\n")
        def templateDir = file("${rootProject.rootDir}/modules/.feature-template")
        def featureDir = file("${rootProject.rootDir}/modules/feature-${placeholders.placeholderlowercase}")

        copy {
            from templateDir
            into featureDir

            // File name replacement
            placeholders.each { placeholderKey, placeholderValue ->
                rename(placeholderKey, placeholderValue)
            }

            // File extension replacement
            rename('.ktemplate', '.kt')

            // Content replacement
            expand(placeholders)
        }

        file("${featureDir}/src/main/kotlin/com/leinardi/androidtemplateproject/placeholderlowercase")
                .renameTo("${featureDir}/src/main/kotlin/com/leinardi/androidtemplateproject/${placeholders.placeholderlowercase}")

        println("""


        Feature created:
            Name: ${placeholders.PlaceholderName}
            Package: com.leinardi.androidtemplateproject.${placeholders.placeholderlowercase}
            Directory: ${featureDir}

        Placeholders used:\n${placeholdersUsed}

        1. Declare the module in settings.gradle (alphabetically ordered):
            // Features
            [...]
            ':modules:feature-${placeholders.placeholderlowercase}',
            [...]

        2. Declare the dependency on the feature on app/build.gradle (alphabetically ordered):
            // Modules
            [...]
            implementation project(':modules:feature-${placeholders.placeholderlowercase}')
            [...]

        3. Synchronize the project for the new module to be available.

        """)

    }
}
